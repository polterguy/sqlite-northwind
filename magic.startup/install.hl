
// Moves all SQL scripts into correct folder.
io.file.list:/modules/sqlite-northwind/magic.startup/
for-each:x:-/*
   strings.split:x:@.dp/#
      .:/
   if
      strings.ends-with:x:@.dp/#
         .:.sql
      .lambda
         strings.concat
            .:/etc/sqlite/templates/
            get-value:x:@strings.split/0/-
         if
            not
               io.file.exists:x:@strings.concat
            .lambda

               // File doesn't exists, hence moving it.
               io.file.move:x:@.dp/#
                  .:/etc/sqlite/templates/
               log.info:SQL script moved into etc folder
                  filename:x:@.dp/#

// Creates the Northwind database file.
if
   not
      io.file.exists:/data/northwind.db
   .lambda

      // Creating a default database file to store database in.
      io.file.save:/data/northwind.db
         .:
      log.info:Northwind database file created
         filename:/data/northwind.db

      // Loads and executes the northwind.sql script.
      io.file.load:/etc/sqlite/templates/northwind.sql
      sqlite.connect:northwind
         sqlite.execute:x:@io.file.load
      log.info:The northwind database was successfully created
         filename:/etc/sqlite/templates/northwind.sql

      // Evicting cache item related to SQLite.
      cache.set:magic.sql.databases.sqlite.generic
